PROJECT(ifc2x3)

SET(IFC2X3_SDK_VERSION_MAJOR 1 CACHE STRING "Version" FORCE)
SET(IFC2X3_SDK_VERSION_MINOR 3 CACHE STRING "Version" FORCE)
SET(IFC2X3_SDK_VERSION_PATCH 0 CACHE STRING "Version" FORCE)
SET(IFC2X3_SDK_VERSION "${IFC2X3_SDK_VERSION_MAJOR}.${IFC2X3_SDK_VERSION_MINOR}" CACHE STRING "Version" FORCE)
SET(IFC2X3_SDK_VERSION_FULL "${IFC2X3_SDK_VERSION}.${IFC2X3_SDK_VERSION_PATCH}" CACHE STRING "Version" FORCE)

SET(IFC2X3_SDK_SOVERSION 1 CACHE STRING "Version" FORCE)

FILE (GLOB PROJECT_SOURCES *.cpp)

FILE (GLOB PROJECT_INCLUDES ../../include/ifc2x3/*.h )
LIST (APPEND PROJECT_INCLUDES ${CMAKE_CURRENT_BINARY_DIR}/ifc2x3/Export.h)

include(GenerateExportHeader)

include_directories(${CMAKE_CURRENT_BINARY_DIR})
include_directories(${Step_BINARY_DIR})

ADD_COMPILER_EXPORT_FLAGS()

ADD_LIBRARY(ifc2x3 ${SHARED_OR_STATIC} ${PROJECT_SOURCES} ${PROJECT_INCLUDES})

GENERATE_EXPORT_HEADER( ifc2x3 EXPORT_FILE_NAME ifc2x3/Export.h STATIC_DEFINE ifc2x3_STATIC)


TARGET_LINK_LIBRARIES(ifc2x3 Step )

IF(MSVC)
  SET_TARGET_PROPERTIES(ifc2x3 PROPERTIES LINK_FLAGS "/INCREMENTAL:NO")
ENDIF(MSVC)

SET_TARGET_PROPERTIES(ifc2x3 PROPERTIES PROJECT_LABEL "Library ifc2x3" VERSION ${IFC2X3_SDK_VERSION_FULL} SOVERSION ${IFC2X3_SDK_SOVERSION})

if(ENABLE_PRECOMPILED_HEADER)
set_target_properties(ifc2x3 PROPERTIES
    COTIRE_PREFIX_HEADER_INCLUDE_PATH "${CMAKE_CURRENT_LIST_DIR}/../../include")
cotire(ifc2x3)
endif()

INSTALL(FILES ${PROJECT_INCLUDES} DESTINATION include/ifc2x3)

INSTALL(TARGETS ifc2x3
        RUNTIME DESTINATION bin
        LIBRARY DESTINATION lib${LIB_POSTFIX}
        ARCHIVE DESTINATION lib${LIB_POSTFIX}
)

if(WIN32)
    install_pdb(ifc2x3 Debug)
    install_pdb(ifc2x3 RelWithDebInfo)
endif()

configure_file(ifc2x3Config.cmake.in ifc2x3Config.cmake @ONLY)

include(CMakePackageConfigHelpers)

WRITE_BASIC_PACKAGE_VERSION_FILE( ifc2x3ConfigVersion.cmake
    VERSION ${ifc2x3_VERSION_MAJOR}.${ifc2x3_VERSION_MINOR}.${ifc2x3_VERSION_PATCH}
    COMPATIBILITY AnyNewerVersion)

if(UNIX)
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ifc2x3Config.cmake ${CMAKE_CURRENT_BINARY_DIR}/ifc2x3ConfigVersion.cmake
            DESTINATION share/ifc2x3
            COMPONENT SDK )
else()
    install(FILES ${CMAKE_CURRENT_BINARY_DIR}/ifc2x3Config.cmake ${CMAKE_CURRENT_BINARY_DIR}/ifc2x3ConfigVersion.cmake
            COMPONENT SDK
            DESTINATION . )
endif()
